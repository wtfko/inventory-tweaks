buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    //id "net.minecraftforge.gradle.forge" version "2.0.2"
    id "com.matthewprenger.cursegradle" version "1.0.8"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'

dependencies {
    compile 'org.jetbrains:annotations:13.0'
}

if(!hasProperty('mod_version')) {
    mod_version = 'UNKNOWN'
}

def getGitCommitHash = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch(ignored) {
        return null;
    }
}

def getGitDirtyIndex = { ->
    try {
        return exec {
            commandLine 'git', 'diff-index', '--quiet', '--cached', 'HEAD'
            ignoreExitValue = true
        }.exitValue == 1
    } catch(ignored) {
        return false;
    }
}

def getGitDirtyFiles = { ->
    try {
        return exec {
            commandLine 'git', 'diff-files', '--quiet'
            ignoreExitValue = true
        }.exitValue == 1
    } catch(ignored) {
        return false;
    }
}

def getGitChangelog = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        def gitHash = System.getenv("GIT_COMMIT")
        def gitPrevHash = System.getenv("GIT_PREVIOUS_COMMIT")
        def travisRange = System.getenv("TRAVIS_COMMIT_RANGE")
        if(gitHash && gitPrevHash) {
            exec {
                commandLine 'git', 'log', '--pretty=tformat:%s - %aN', '' + gitPrevHash + '...' + gitHash
                standardOutput = stdout
            }
            return stdout.toString().trim()
        } else if(travisRange) {
            exec {
                commandLine 'git', 'log', '--pretty=tformat:%s - %aN', '' + travisRange
                standardOutput = stdout
            }
            return stdout.toString().trim()
        } else {
            return "";
        }
    } catch(ignored) {
        return "";
    }
}

ext.git_version = getGitCommitHash()
ext.git_dirty = getGitDirtyIndex() || getGitDirtyFiles()

ext.build_number = System.getenv('BUILD_NUMBER')
if(!build_number) {
    ext.build_number = System.getenv('TRAVIS_BUILD_NUMBER')
}

ext.git_separator = '.'
if(mod_version.contains('+')) {
    if(build_number) {
        version = mod_version + '.' + build_number
    } else {
        version = mod_version
    }

    ext.internal_version = version
} else {
    version = mod_version

    ext.internal_version = version
    if(build_number) {
        ext.internal_version += "+release." + build_number
    } else {
        ext.git_separator = '+'
    }
}

if(git_version) {
    ext.internal_version += git_separator + git_version
    if(git_dirty) {
        ext.internal_version += '.dirty'
    }
} else {
    ext.internal_version = version
}

group = mod_group
archivesBaseName = mod_name

minecraft {
    version = forge_version
    mappings = mcp_version

    runDir = "run"
    makeObfSourceJar = false

    replace '@VERSION@', project.internal_version
    replaceIn 'InvTweaksConst.java'
}

if(hasProperty('mod_core_plugin')) {
    minecraft.coreMod = mod_core_plugin

    jar {
        manifest {
            attributes 'FMLCorePlugin': mod_core_plugin, 'FMLCorePluginContainsFMLMod': mod_core_hasfmlmod
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.debug = true
    options.debugOptions.debugLevel = 'source,lines,vars'
}

task apiJar(type: Jar) {
    onlyIf { project.hasProperty('api_filter') }
    dependsOn classes

    from sourceSets.main.allJava
    from sourceSets.main.output
    include api_filter

    classifier 'api'
}

task deobfJar(type: Jar) {
    dependsOn classes

    from sourceSets.main.output

    if(project.hasProperty('mod_core_plugin')) {
        manifest {
            attributes 'FMLCorePlugin': mod_core_plugin, 'FMLCorePluginContainsFMLMod': mod_core_hasfmlmod
        }
    }

    classifier 'deobf'
}

artifacts {
    archives apiJar
    archives deobfJar
}

ext.curseKey = System.getenv("CURSE_API_KEY")
if(mod_version.endsWith("dev")) {
    ext.curseType = 'alpha'
    ext.curseVersion = version
} else if(mod_version.endsWith("beta")) {
    ext.curseType = 'beta'
    ext.curseVersion = version
} else {
    ext.curseType = 'release'
    ext.curseVersion = mod_version
}

tasks.curseforge.onlyIf { curseKey }
tasks.curseforge.dependsOn jar, apiJar, deobfJar

if(hasProperty('curse_extra_versions')) {
    ext.curseExtraVersions = curse_extra_versions.tokenize(',')
}

if(curseKey && hasProperty('curse_id')) {
    curseforge {
        apiKey = curseKey

        project {
            id = curse_id
            changelog = getGitChangelog()
            releaseType = curseType
            addGameVersion minecraft.version

            if(hasProperty('curseExtraVersions')) {
                for(version in curseExtraVersions) {
                    addGameVersion version
                }
            }
            addGameVersion 'Java 8'

            mainArtifact(jar) {
                displayName = "$curse_filename $curseVersion"
            }

            addArtifact(apiJar) {
                displayName = "$curse_filename API $curseVersion"
            }

            addArtifact(deobfJar) {
                displayName = "$curse_filename Deobfuscated $curseVersion"
            }
        }
    }
}

processResources {
    inputs.property 'version', internal_version
    inputs.property 'mcversion', minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.png'
        exclude '**/*_at.cfg'

        expand version: internal_version, mcversion: minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.png'
        include '**/*_at.cfg'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }

    project {
        vcs = 'Git'
    }
}
